// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSGrassCullingHeight
#pragma kernel CSGrassCullingMiddle
#pragma kernel CSGrassCullingLow
#pragma kernel CSGrassCullingStatic

#include "UnityCG.cginc"

struct instancing_data
{
    float4x4 trs;
    float4 wind;
    float4 bend;
};

struct instancing_data_12
{
    float4x4 trs;
    float4 wind;
};


float4 _CameraPlanes[6];
float4x4 _CameraVPMatrix;         //camera vp matrix.
float4 _BoundPoints[8];
Texture2D _HizDepthTexture;

float _Wind;
float _WindGap;
float4 _WindDir;        //w=sqrt(AA+BB+CC)
Texture2D _WindNoise;


uint _BendCount;
float4 _BendInfos[8];
float _BendDelta;
RWStructuredBuffer<float4> _BendBuffer;

uint _InstanceCount;
StructuredBuffer<float4x4> _InstancingBuffer;
AppendStructuredBuffer<instancing_data> _CullResult;
AppendStructuredBuffer<instancing_data_12> _CullResult12;
AppendStructuredBuffer<float4x4> _CullResult3;

inline bool IsInClipSpace(float4 clip_pos)
{
    return clip_pos.x > -clip_pos.w && clip_pos.x < clip_pos.w
    && clip_pos.y > -clip_pos.w && clip_pos.y < clip_pos.w
    && clip_pos.z > 0 && clip_pos.z < clip_pos.w;
}

bool IsCulling(float4x4 trs)
{
    //view frushtum culling
    bool in_clip = false;
    float4x4 mvpMatrix = mul(_CameraVPMatrix, trs);
    float4 cps[8];
    for (int i = 0; i < 8; i++)
    {
        cps[i] = mul(mvpMatrix, _BoundPoints[i]);
        in_clip = in_clip || IsInClipSpace(cps[i]);
    }

    bool depth_culling = false;
    if (in_clip)
    {
        //Hiz depth culling
        float3 min_p = float3(1, 1, 1);             //AABB for NDC
        float3 max_p = float3(-1, -1, -1);
        for (int i = 0; i < 8; i++)
        {
            float4 clip_pos = cps[i];
            float3 ndc = clip_pos.xyz / clip_pos.w;
            min_p = min(min_p, ndc);
            max_p = max(max_p, ndc);
        }

        //ncd[-1,1] to uv[0,1]
        float2 uv_center = (min_p + max_p).xy / 2 * 0.5f + 0.5f;

        //select mipmap level
        uint tex_size = 256;
        uint mipmap_level = (uint)clamp(log2(max(max_p.x - min_p.x, max_p.y - min_p.y) * tex_size) + 1, 0, 4);      //size[16,256]->mip[4,0]
        uint size = tex_size / (1 << mipmap_level);

        //compare depth value culling
        uint2 pixel_center = uint2(clamp(uv_center.x * size, 0, size - 1), clamp(uv_center.y * size, 0, size - 1));
        float depth_tex = _HizDepthTexture.mips[mipmap_level][pixel_center].r;
        depth_culling = max_p.z < depth_tex;      //The smaller the farther
    }
    return !in_clip || depth_culling;
}

bool IsCullingSimple(float4x4 trs)
{
    //view frushtum culling
    bool in_clip = false;
    float4x4 mvpMatrix = mul(_CameraVPMatrix, trs);
    for (int i = 0; i < 8 && !in_clip; i++)
    {
        float4 cp = mul(mvpMatrix, _BoundPoints[i]);
        in_clip = IsInClipSpace(cp);
    }
    return !in_clip;
}

fixed4 Tex2DWrap(Texture2D tex, uint2 size, float2 uv)
{
    uv = frac(1+frac(uv));      //set uv to 0-1
    float2 pos = uv * size;
    int2 start = int2(pos.x, pos.y);
    int2 end = int2((start.x+1)%size.x, (start.y+1)%size.y);
    float2 f = float2(frac(pos.x), frac(pos.y));
    float4 c_0 = tex[start];
    float4 c_1 = tex[int2(end.x, start.y)];
    float4 c_2 = tex[int2(start.x, end.y)];
    float4 c_3 = tex[end];
    float4 c_01 = lerp(c_0, c_1, f.x);
    float4 c_23 = lerp(c_2, c_3, f.x);
    return lerp(c_01, c_23, f.y);
}

float4 GetWind(float3 pos)
{
    float4 wind = float4(0, 0, 0, 0);
    float2 noise_uv = (-_WindDir.xz * _Wind + pos.xz) / _WindGap;       //UV move in the opposite direction
    float rate = Tex2DWrap(_WindNoise, uint2(256, 256), noise_uv).r;
    wind.xyz = normalize(_WindDir.xyz + _WindDir.zyx * (rate - 0.5));         //swing
    wind.w = rate;
    return wind;
}

float4 GetBend(float3 pos)
{
    float4 bend_dir = float4(0, 0, 0, 0);
    for (uint i=0; i<_BendCount; ++i)
    {
        float4 ri = _BendInfos[i];
        float3 dir = pos - ri.xyz;
        float bend_max = 1.0 * ri.w;
        float bend_min = 0.5 * ri.w;
        float bend_dis_2 = dot(dir,dir);
        if (bend_dis_2 < bend_max * bend_max)
        {
            float br = 1 - smoothstep(bend_min, bend_max, sqrt(bend_dis_2));
            bend_dir.xz += normalize(dir.xz) * br;
            bend_dir.w = max(bend_dir.w, br);
        }
    }
    if (bend_dir.w > 0)
    {
        bend_dir.xz = normalize(bend_dir.xz);
        bend_dir.y = -0.4;
        bend_dir.xyz = normalize(bend_dir.xyz);
    }    
    return bend_dir;
}

[numthreads(512, 1, 1)]
void CSGrassCullingHeight (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= _InstanceCount)
    {
        return;
    }

    //only view frushtum culling for mobile is faster
    float4x4 trs = _InstancingBuffer[id.x];
    bool in_clip = false;
    float4 bend = _BendBuffer[id.x];
    float4x4 mvpMatrix = mul(_CameraVPMatrix, trs);
    for (int i = 0; i < 8 && !in_clip; i++)
    {
        float4 cp = mul(mvpMatrix, _BoundPoints[i]);
        in_clip = IsInClipSpace(cp);
    }
    if (!in_clip)
    {
        bend.w = max(0, bend.w - _BendDelta);
        _BendBuffer[id.x] = bend;
        return;
    }

    //compute wind
    float3 pos = trs._14_24_34;        //_14_24_34 = position
    float4 wind = GetWind(pos);
    float4 new_bend = GetBend(pos);
    if (new_bend.w < bend.w)
    {
        bend.w = max(0, bend.w - _BendDelta);
        new_bend = bend;
    }
    _BendBuffer[id.x] = new_bend;

    float all = wind.w + new_bend.w;
    if (all > 0)
    {
        wind.w *= wind.w / all;
        new_bend.w *= new_bend.w / all;
    }
    
    instancing_data data;
    data.trs = trs;
    data.wind = wind;
    data.bend = new_bend;
    _CullResult.Append(data);
}

[numthreads(512, 1, 1)]
void CSGrassCullingMiddle (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= _InstanceCount)
    {
        return;
    }

    //only view frushtum culling for mobile is faster
    float4x4 trs = _InstancingBuffer[id.x];
    bool in_clip = false;
    float4x4 mvpMatrix = mul(_CameraVPMatrix, trs);
    for (int i = 0; i < 8 && !in_clip; i++)
    {
        float4 cp = mul(mvpMatrix, _BoundPoints[i]);
        in_clip = IsInClipSpace(cp);
    }
    if (!in_clip)
    {
        return;
    }

    //compute wind
    float3 pos = trs._14_24_34;        //_14_24_34 = position
    instancing_data_12 data;
    data.trs = trs;
    data.wind = GetWind(pos);
    _CullResult12.Append(data);
}

[numthreads(512, 1, 1)]
void CSGrassCullingLow (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= _InstanceCount)
    {
        return;
    }

    //only view frushtum culling for mobile is faster
    float4x4 trs = _InstancingBuffer[id.x];
    bool in_clip = false;
    float4x4 mvpMatrix = mul(_CameraVPMatrix, trs);
    for (int i = 0; i < 8 && !in_clip; i++)
    {
        float4 cp = mul(mvpMatrix, _BoundPoints[i]);
        in_clip = IsInClipSpace(cp);
    }
    if (!in_clip)
    {
        return;
    }

    //compute wind
    float3 pos = trs._14_24_34;        //_14_24_34 = position
    instancing_data_12 data;
    data.trs = trs;
    data.wind = GetWind(pos);
    _CullResult12.Append(data);
}

[numthreads(512, 1, 1)]
void CSGrassCullingStatic (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= _InstanceCount)
    {
        return;
    }

    //only view frushtum culling for mobile is faster
    float4x4 trs = _InstancingBuffer[id.x];
    bool in_clip = false;
    float4x4 mvpMatrix = mul(_CameraVPMatrix, trs);
    for (int i = 0; i < 8 && !in_clip; i++)
    {
        float4 cp = mul(mvpMatrix, _BoundPoints[i]);
        in_clip = IsInClipSpace(cp);
    }
    if (!in_clip)
    {
        return;
    }

    _CullResult3.Append(trs);
}